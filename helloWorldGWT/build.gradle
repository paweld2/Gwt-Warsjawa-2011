apply plugin: 'war'
apply plugin: 'jetty'

sourceCompatibility = 1.5
version = '0.1'
group = 'pl.warsjawa'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

dependencies {
    compile (
        [group: 'com.google.gwt', name: 'gwt-user', version: '2.4.0'],
        [group: 'com.google.gwt', name: 'gwt-dev', version: '2.4.0'],
        [group: 'com.google.gwt', name: 'gwt-servlet', version: '2.4.0'],
    )
    testCompile group: 'junit', name: 'junit', version: '4.10'
}


gwtBuildDir = 'war'
webAppDirName = 'war'

task gwtCompile << {  
	created = (new File(gwtBuildDir)).mkdirs()  
	ant.java(classname:'com.google.gwt.dev.Compiler', failOnError: 'true', fork: 'true') 
	{    
		jvmarg(value: '-Xmx184M')    
		arg(line: '-war ' + gwtBuildDir)
		arg(line: '-logLevel INFO')    
		arg(line: '-style PRETTY')    
		arg(value: 'pl.ja.Test')    
		classpath {      
			pathElement(location: 'src')      
			pathElement(path: configurations.compile.asPath)       
		}  
	}
}

task(devMode, dependsOn: 'classes', type: JavaExec) {
    main = 'com.google.gwt.dev.DevMode'
    classpath {
        [ sourceSets.main.java.srcDirs, sourceSets.main.runtimeClasspath ]
    }
    args '-startupUrl', 'Test.html', '-port' , '8888' ,  '-war', "$gwtBuildDir", "pl.ja.Test"
}

task copyResourcesToWar(type: Copy) {
    from 'resources/content'
    into 'war'
}
 
war.dependsOn gwtCompile
war.dependsOn copyResourcesToWar
 
war {
	webInf { from 'resources/content/WEB-INF' }
    from gwtBuildDir
    classpath configurations.runtime
}

stopKey = 'foo'
stopPort = 9451
httpPort = 8163

uploadArchives {
    repositories {
       add rootProject.repositories.gwtRepoLocal
    }
}
